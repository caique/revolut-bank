plugins {
    id 'java'
    id 'application'
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.glassfish.jersey.containers:jersey-container-grizzly2-http:2.28'
    compile 'org.glassfish.jersey.inject:jersey-hk2:2.28'
    compile 'org.glassfish.jersey.media:jersey-media-json-jackson:2.28'

    compile 'org.json:json:20180813'
    compile 'com.google.guava:guava:27.1-jre'

    testCompile 'junit:junit:4.12'
    testCompile 'org.assertj:assertj-core:3.12.2'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testCompile 'org.glassfish.jersey.core:jersey-client:2.28'
}

defaultTasks('clean', 'test', 'build')

mainClassName = 'com.revolut.bank.Application'
group = 'com.revolut.bank'
version = "1.0.0"

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration/java')
        }

        resources.srcDir file('src/integration/resources')
    }
}

configurations {
    integrationCompile.extendsFrom testCompile
    integrationRuntime.extendsFrom testRuntime
}

task integrationTest(type: Test) {
    description = 'Runs the integration tests.'
    group = JavaBasePlugin.VERIFICATION_GROUP
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn(integrationTest)
